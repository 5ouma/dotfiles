#!/usr/bin/env zsh

set -euo pipefail

#==================================================[ Variables ]==================================================#

export XDG_CONFIG_HOME="$HOME/.config"
export XDG_CACHE_HOME="$HOME/.cache"
export XDG_DATA_HOME="$HOME/.local/share"
export ZDOTDIR="$XDG_CONFIG_HOME/zsh"

declare -r githubUrl='https://github.com/5ouma/dotfiles'
declare -r dotfiles="$HOME/.dotfiles"
declare -r config="$dotfiles/config"
declare -r data="$dotfiles/data"
declare -r TMPDIR="${TMPDIR:-"$(mktemp -d)"}"
declare -r GIT_REF="${GITHUB_HEAD_REF:-main}"

declare executed=false
declare skipped=false
declare goThrough=false

#==================================================[ Functions ]==================================================#

# Actions
getGum() {
  declare -r gumVir='0.15.0'
  declare -r gumDir="gum_${gumVir}_$(uname)_$(uname -m)"
  declare -r gumUrl="https://github.com/charmbracelet/gum/releases/download/v$gumVir/$gumDir.tar.gz"
  declare -r gumThemeUrl="https://raw.githubusercontent.com/5ouma/dotfiles/$GIT_REF/data/blue.json"

  if (! type gum &>/dev/null); then
    printf '  üööüí® Now delivering...'
    curl -sL "$gumUrl" | tar --strip-components=1 -zxC "$TMPDIR" "$gumDir/gum" && alias gumCom="$TMPDIR/gum"
    printf '\r                        '
  else
    alias gumCom="$(which gum)"
  fi

  if [[ ! -f "$data/blue.json" ]]; then
    gumTheme=$(curl -s "$gumThemeUrl") && declare -gr gumTheme
  else
    gumTheme=$(cat "$data/${gumThemeUrl/*\//}") && declare -gr gumTheme
  fi
} && getGum

gum() {
  case $1 in
  confirm) gumCom confirm "$2" --selected.background='27' --affirmative="$3" --negative="$4" ;;
  format) gumCom format --theme=<(echo -E "$gumTheme") "${@:2}" ;;
  run)
    if (tty &>/dev/null); then
      gumCom spin --spinner.foreground='27' --title="$2" -- "${@:4}"
    else
      echo && gumCom log --level='info' "$2"
      "${@:4}"
    fi
    ;;
  style) gumCom style --foreground="$2" "${@:3}" ;;
  executed)
    echo -en '\n  ' && gumCom format '‚úÖ Done!'
    executed=true
    ;;
  skipped) echo -en '\n  ' && gumCom format 'üü° Skipped' ;;
  esac
}

getDot() {
  if [[ ! -d "$dotfiles" ]]; then
    # shellcheck disable=SC2016
    gum run 'Cloning the repository into `~/.dotfiles`...' -- git clone "$githubUrl" -b "$GIT_REF" "$dotfiles"
  else
    gum run 'Pulling the dotfiles repository...' -- git -C "$dotfiles" pull
  fi
}

checkSym() {
  declare -Ag cfgs
  while read -r cfg; do
    declare file="${cfg#"$config"/*/}"
    if [[ ! -f "$HOME/$file" ]] || (! diff "$cfg" "$HOME/$file" &>/dev/null); then
      cfgs[$file]=$cfg
    fi
  done < <(find "$config" -type f ! -name '.DS_Store')
  ((${#cfgs} > 0))
}

mkSym() {
  # shellcheck disable=SC2296
  while read -r file; do
    mkdir -p "$(dirname "$HOME/$file")"
    if [[ -f "$HOME/$file" || -L "$HOME/$file" ]]; then
      mkdir -p "$HOME/.backup"
      mv "$HOME/$file" "$HOME/.backup/$(date +'%H%M%S')_$(basename "$file")"
    fi
    ln -s "${cfgs[$file]}" "$HOME/$file"
    # shellcheck disable=SC2088
    printf "- \033[36m%s\033[m\n" "~/$file"
  done < <(printf "%s\n" "${(k)cfgs[@]}" | sort) | gum format
  unset cfgs
}

# Utilities
alias skip='skipped=true && return'
run() {
  declare -r func="${1:u}"
  gum format "$2"
  (${3:-false} && ! isDarwin) && eval 'declare SETUP_SKIP_'"$func"'=true'
  if (eval '${SETUP_SKIP_'"$func"':-false}'); then
    gum skipped
    return
  fi

  eval '$1'
  if ($skipped); then
    skipped=false
    gum skipped
  else
    gum executed
  fi
}

isDarwin() {
  [[ "$(uname)" == 'Darwin' ]]
}

helpMsg() {
  gum format <<EOM
## Usage: setup

> ‚öôÔ∏è The most minimal and powerful dotfiles ever.

## Commands:
-  help    Print help information
-  set     Set files to home directory

## Flags:
-  -y, --yes    Start without confirmation
EOM
}

errMsg() {
  declare -r stat=$1 func="${2/"$HOME"/~}" line=$3
  gum format <<EOM
> **$(gum style 1 'Error')**
> Unknown error has occurred!

> - Status:           $stat
> - File or Function: $func
> - Line Number:      $line
> - Process ID:       $$
EOM
} && trap 'errMsg $? "$0" $LINENO' ERR

#==================================================[ Welcome ]==================================================#

if [[ ! -x $0 ]]; then
  gum format <<EOM
# Welcome to ‚öôÔ∏è dotfiles setup!
> $githubUrl
---
EOM
fi

#==================================================[ Flags ]==================================================#

if (($# == 0)); then
  if (! isDarwin); then
    gum format <<EOM
> **$(gum style 3 'Warning')**
> Homebrew requires some dependencies on Linux.
> [](https://docs.brew.sh/Homebrew-on-Linux#requirements)
EOM
    gum confirm 'Are you ready to run setup?' 'Yes!' 'No' || exit 1
  fi
else
  case "$1" in
  'help')
    helpMsg
    exit
    ;;
  'set')
    getDot && checkSym && mkSym
    exit
    ;;
  '-y' | '--yes')
    goThrough=true
    export NONINTERACTIVE=1
    ;;
  *)
    gum format <<EOM
> **$(gum style 1 'Error')**
> Unknown flag $1
EOM
    helpMsg
    exit 1
    ;;
  esac
fi

#==================================================[ Preparations ]==================================================#

# Ask to confirm
if (! $goThrough); then
  gum confirm "Let's start setup!" 'Yes!' 'No' || exit 1
fi

# Install Command Line Tools
if (isDarwin && ! xcode-select -p &>/dev/null); then
  xcode-select --install
  gum confirm 'Continue when you done' 'Done' ''
fi

# Clone / Pull the repository
getDot

#==================================================[ Homebrew ]==================================================#

installHomebrew() {
  (type brew &>/dev/null) && skip
  /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
  if (isDarwin); then
    eval "$(/opt/homebrew/bin/brew shellenv)"
  else
    eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
  fi
} && run 'installHomebrew' '## üç∫ Install Homebrew'

#==================================================[ Files ]==================================================#

setSymlink() {
  if ! checkSym; then
    skip
  fi
  mkSym
} && run 'setSymlink' '## üîó Symlink files'

#==================================================[ Settings ]==================================================#

# shellcheck disable=SC2016
enableTouchID() {
  [[ -f '/etc/pam.d/sudo_local' ]] && skip
  sed -e 's/^#auth/auth/' /etc/pam.d/sudo_local.template | sudo tee /etc/pam.d/sudo_local >/dev/null
} && run 'enableTouchID' '## üëë Enable Touch ID for `sudo`' true

changeLaunchpadSize() {
  declare -r springboardColumns="$(defaults read com.apple.dock springboard-columns)" springboardRows="$(defaults read com.apple.dock springboard-rows)" &>/dev/null
  [[ $springboardColumns == 9 && $springboardRows == 8 ]] && skip
  defaults write com.apple.dock springboard-columns -int 9
  defaults write com.apple.dock springboard-rows -int 8
  killall Dock
} && run 'changeLaunchpadSize' '## üöÄ Change the Launchpad size' true

makeDockImmutable() {
  declare -r sizeImmutable="$(defaults read com.apple.dock size-immutable)" &>/dev/null
  [[ "$sizeImmutable" == 1 ]] && skip
  defaults write com.apple.dock size-immutable -bool true
} && run 'makeDockImmutable' '## üö• Make Dock size immutable' true

changeScreenCapturePath() {
  declare -r screenCaptureLocation="$(defaults read com.apple.screencapture location)" screenCaptures="$HOME/Pictures/Screen Captures" &>/dev/null
  [[ "$screenCaptureLocation" == "$screenCaptures" ]] && skip
  mkdir -p "$screenCaptures"
  defaults write com.apple.screencapture location "$screenCaptures"
} && run 'changeScreenCapturePath' '## üì∑ Change screen capture path' true

setComputerName() {
  declare -r modelName="$(system_profiler SPHardwareDataType | grep 'Model Name' | cut -d ':' -f 2 | xargs)"
  declare -r computerName="$(id -F)'s $modelName" localHostName="$(id -F)s-${modelName// /-}"
  [[ "$(scutil --get ComputerName)" == "$computerName" && "$(scutil --get HostName)" == "$computerName" && "$(scutil --get LocalHostName)" == "$localHostName" ]] && skip
  gum run 'Setting ComputerName...' -- sudo scutil --set ComputerName "$computerName"
  gum run 'Setting HostName...' -- sudo scutil --set HostName "$computerName"
  gum run 'Setting LocalHostName...' -- sudo scutil --set LocalHostName "$localHostName"
  gum format <<EOM
- ComputerName: $(gum style 6 "$(sudo scutil --get ComputerName)")
- HostName: $(gum style 6 "$(sudo scutil --get HostName)")
- LocalHostName: $(gum style 6 "$(sudo scutil --get LocalHostName)")
EOM
} && run 'setComputerName' '## üíª Set the computer name' true

#==================================================[ Apps ]==================================================#

installApps() {
  (! isDarwin) && export HOMEBREW_BUNDLE_BREW_SKIP='newrelic-infra-agent 5ouma/formula/mksei 5ouma/formula/mli blacktop/tap/lporg lslqtz/formulae/bclm_loop'
  (gum run "Satisfying Brewfile's dependencies..." -- brew bundle check --file="$data/Brewfile") && skip
  brew update && brew bundle install --no-lock --file="$data/Brewfile"
  if (isDarwin); then
    sudo xcodebuild -license accept
  fi
} && run 'installApps' '## üì≤ Install commands and apps'

changeLoginShell() {
  [[ "${SHELL:-''}" == "$(which zsh)" ]] && skip
  which zsh | sudo tee -a /etc/shells >/dev/null
  sudo chsh -s "$(which zsh)" "$USER"
} && run 'changeLoginShell' '### üêö Change login shell to Zsh'

restoreLaunchpad() {
  lporg save -c="$TMPDIR/lporg.yml" &>/dev/null
  (diff "$data/lporg.yml" "$TMPDIR/lporg.yml" &>/dev/null) && skip
  # shellcheck disable=SC2016
  gum run 'Running `lporg load`...' -- lporg load -nyc="$data/lporg.yml" >/dev/null || :
} && run 'restoreLaunchpad' '## üöÄ Restore Launchpad and Dock' true

restoreAppSettings() {
  [[ -z "$(mackup -n backup 2>/dev/null)" && $? ]] && skip
  mackup restore
} && run 'restoreAppSettings' '## üíæ Restore application settings' true

restoreAppShortcuts() {
  (defaults read com.apple.universalaccess com.apple.custommenu.apps &>/dev/null) && skip
  # shellcheck disable=SC2016
  gum run 'Running `mksei load`...' -- mksei load "$data/keyboard_shortcuts.json"
} && run 'restoreAppShortcuts' '## ‚å®Ô∏è Restore App Shortcuts' true

restoreLoginItems() {
  (mli check --file="$data/login_items.json" &>/dev/null) && skip
  # shellcheck disable=SC2016
  gum run 'Running `mli load`...' -- mli load --file="$data/login_items.json"
} && run 'restoreLoginItems' '## üìë Restore Login Items' true

limitBatteryCharge() {
  sudo bclm_loop persist
} && run 'limitBatteryCharge' '## üîã Limit battery charge' true

generateGitHooks() {
  [[ -f "$XDG_CONFIG_HOME/git/hooks/pre-commit" && -f "$XDG_CONFIG_HOME/git/hooks/commit-msg" ]] && skip
  pre-commit init-templatedir "$XDG_CONFIG_HOME/git" -c="$data/pre-commit.yml" | sed -n 1p
  gitlint --target="$dotfiles" install-hook
} && run 'generateGitHooks' '## ü™ù Generate Git Hooks for pre-commit and commit-msg'

loginGitHubCli() {
  (gh auth status) && skip
  gh auth login -p='SSH' -h='GitHub.com' -s='codespace,delete_repo,workflow' --skip-ssh-key -w
} && run 'loginGitHubCli' '## üêô Log in to GitHub CLI'

installProgrammingLanguages() {
  eval "$(mise activate -q zsh)"
  [ -z "$(mise ls --missing)" ] && skip
  mise install
} && run 'installProgrammingLanguages' '## üìº Install programming languages'

installVimPlugins() {
  [ -d "$XDG_DATA_HOME/vim/pack/jetpack/opt" ] && skip
  vim +Jetpack +qall
  gum run 'Building a binary of vim-lumen...' -- vim +qall!
} && run 'installVimPlugins' '## üñå Install Vim plugins'

installYaziPlugins() {
  [ -d "$XDG_DATA_HOME/yazi/packages" ] && skip
  ya pack -i
} && run 'installYaziPlugins' '## üê§ Install Yazi plugins'

#==================================================[ Done! ]==================================================#

if ($executed); then
  (isDarwin) && killall Dock SystemUIServer
  gum format '# üéâ Setup completed!'
else
  gum format '# üßä Nothing has changed'
fi
