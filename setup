#!/usr/bin/env zsh

set -eu

#==================================================[ Variables ]==================================================#

export XDG_CONFIG_HOME="$HOME/.config"
export XDG_CACHE_HOME="$HOME/.cache"
export XDG_DATA_HOME="$HOME/.local/share"
export ZDOTDIR="$XDG_CONFIG_HOME/zsh"

declare -r githuburl='https://github.com/5ouma/dotfiles.git'
declare -r dotfiles="$HOME/.dotfiles"
declare -r packages="$dotfiles/packages"
declare -r datas="$dotfiles/datas"
declare -r TMPDIR="${TMPDIR:-"$(mktemp -d)"}"

declare executed=false
declare gothrough=false

#==================================================[ Functions ]==================================================#

# Actions
getGum() {
  declare -r gumurl="https://github.com/charmbracelet/gum/releases/latest/download/gum_0.11.0_$(uname)_$(uname -m).tar.gz"
  declare -r gumthemeurl='https://raw.githubusercontent.com/5ouma/dotfiles/HEAD/datas/blue.json'

  if (! type gum >/dev/null 2>&1); then
    printf '  üööüí® Now delivering...'
    curl -sL "$gumurl" | tar -zxC "$TMPDIR" 'gum' && declare -gr gumcom="$TMPDIR/gum"
    printf '\r                        '
  else
    gumcom=$(which gum) && declare -gr gumcom
  fi

  if [[ ! -f "$datas/blue.json" ]]; then
    gumtheme=$(curl -s "$gumthemeurl") && declare -gr gumtheme
  else
    gumtheme=$(cat "$datas/${gumthemeurl/*\//}") && declare -gr gumtheme
  fi
}
getGum

gum() {
  case $1 in
  confirm) $gumcom confirm "$2" --selected.background='27' --affirmative="$3" --negative="$4" ;;
  format) $gumcom format --theme=<(echo -E "$gumtheme") "${@:2}" ;;
  spin) $gumcom spin --spinner.foreground='27' --title="$2" "${@:3}" ;;
  style) $gumcom style --foreground="$2" "${@:3}" ;;
  executed)
    echo && $gumcom format '‚úÖ Done!'
    executed=true
    ;;
  esac
}

getDot() {
  if [[ ! -d "$dotfiles" ]]; then
    # shellcheck disable=SC2016
    gum spin 'Cloning the repository into `~/.dotfiles`...' -- git clone "$githuburl" "$dotfiles"
  else
    gum spin 'Pulling the dotfiles repository...' -- git -C "$dotfiles" pull
  fi
}

checkSym() {
  declare -Ag packs
  while read -r pack; do
    declare file="${pack#"$packages"/*/}"
    if [[ ! -f "$HOME/$file" ]] || (! diff "$pack" "$HOME/$file" >/dev/null 2>&1); then
      packs[$file]=$pack
    fi
  done < <(find "$packages" -type f ! -name '.DS_Store')
  ((${#packs} > 0))
}

mkSym() {
  # shellcheck disable=SC2296
  while read -r file; do
    mkdir -p "$(dirname "$HOME/$file")"
    ln -is "${packs[$file]}" "$HOME/$file"
    # shellcheck disable=SC2088
    echo "- $(gum style 6 "~/$file")"
  done < <(printf "%s\n" "${(k)packs[@]}" | sort) | gum format
}

# Utilities
helpMsg() {
  gum format <<EOM
## Usage: setup

> ‚öôÔ∏è The most minimal and powerful dotfiles ever.

## Flags:
-  -h, --help    Print help information
-  -y, --yes     Start without confirmation
-  -s, --set     Set files to home directory
EOM
}

isDarwin() {
  [ "$(uname)" = 'Darwin' ]
}

confirmNonDarwin() {
  if (! isDarwin); then
    gum format <<EOM
> **$(gum style 3 'Warning')**
> Homebrew requires some dependencies on Linux.
> [](https://docs.brew.sh/Homebrew-on-Linux#requirements)
EOM
    gum confirm 'Are you ready to run setup?' 'Yes!' 'No' || exit 1
  fi
}

#==================================================[ Welcome ]==================================================#

if [[ ! -x $0 ]]; then
  gum format <<EOM
# Welcome to ‚öôÔ∏è dotfiles setup!
> https://github.com/5ouma/dotfiles
---
EOM
fi

#==================================================[ Flags ]==================================================#

if (($# == 0)); then
  confirmNonDarwin
else
  case "$1" in
  '-h' | '--help')
    helpMsg
    exit
    ;;
  '-y' | '--yes') gothrough=true ;;
  '-s' | '--set')
    getDot && checkSym && mkSym
    exit
    ;;
  *)
    gum format <<EOM
> **$(gum style 1 'Error')**
> Unknown flag $1
EOM
    helpMsg
    exit 1
    ;;
  esac
fi

#==================================================[ Preperations ]==================================================#

# Ask to confirm
if (! $gothrough); then
  gum confirm "Let's start setup!" 'Yes!' 'No' || exit 1
fi

# Clone / Pull the repository
getDot

# Zsh setting
zstyle ':completion:*:commands' rehash 1

#==================================================[ Homebrew ]==================================================#

if (! type brew >/dev/null 2>&1); then
  gum format '## üç∫ Installing Homebrew...'
  /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
  if (isDarwin); then
    [ "$(uname -m)" = 'x86_64' ] && eval "$(/usr/local/bin/brew shellenv)"
    [ "$(uname -m)" = 'arm64' ] && eval "$(/opt/homebrew/bin/brew shellenv)"
  else
    eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
  fi
  brew doctor
  gum executed
fi

#==================================================[ Configs ]==================================================#

if checkSym; then
  gum format '## üîó Symlinking files...'
  mkSym
  gum executed
fi

if [[ ! -f "$HOME/.ssh/git/github.pub" || ! -f "$HOME/.ssh/git/github-school.pub" ]]; then
  gum format '## üìÉ Downloading SSH public keys...'
  mkdir -p "$HOME/.ssh/git"
  curl -s https://github.com/5ouma.keys | head -n 1 >"$HOME/.ssh/git/github.pub"
  curl -s https://github.com/6ouma.keys | head -n 1 >"$HOME/.ssh/git/github-school.pub"
  gum executed
fi

#==================================================[ defaults ]==================================================#

if (isDarwin); then
  springboard_columns=$(defaults read com.apple.dock springboard-columns) springboard_rows=$(defaults read com.apple.dock springboard-rows) >/dev/null 2>&1 && declare -r springboard_{columns,rows}
  if ((springboard_columns != 9 || springboard_rows != 8)); then
    gum format '## üöÄ Changing Launchpad size...'
    defaults write com.apple.dock springboard-columns -int 9
    defaults write com.apple.dock springboard-rows -int 8
    gum executed
  fi

  size_immutable=$(defaults read com.apple.dock size-immutable) >/dev/null 2>&1 && declare -r size_immutable
  if ((size_immutable != 1)); then
    gum format '## üö• Making Dock size immutable...'
    defaults write com.apple.dock size-immutable -bool true
    gum executed
  fi

  screencapture_location="$(defaults read com.apple.screencapture location)" >/dev/null 2>&1 && declare -r screencapture_location
  if [[ "$screencapture_location" != "$HOME/Pictures/Screen Capture" ]]; then
    gum format '## üì∑ Changing screen capture path...'
    mkdir -p "$HOME/Pictures/Screen Capture"
    defaults write com.apple.screencapture location "$HOME/Pictures/Screen Capture"
    gum executed
  fi

  killall Dock
  killall SystemUIServer

  modelname="$(system_profiler SPHardwareDataType | grep 'Model Name' | sed "s/.*: //")" && declare -r modelname
  computername="$(id -F)'s $modelname" localhostname="$(id -F)s-${modelname// /-}" && declare -r computername localhostname
  if [[ "$(scutil --get ComputerName)" != "$computername" || "$(scutil --get HostName)" != "$computername" || "$(scutil --get LocalHostName)" != "$localhostname" ]]; then
    gum format '## üíª Setting computer name...'
    gum spin 'Setting ComputerName...' -- scutil --set ComputerName "$computername"
    gum spin 'Setting HostName...' -- scutil --set HostName "$computername"
    gum spin 'Setting LocalHostName...' -- scutil --set LocalHostName "$localhostname"
    sudo -v
    gum format <<EOM
- ComputerName : $(gum style 6 "$(scutil --get ComputerName)")
- HostName : $(gum style 6 "$(scutil --get HostName)")
- LocalHostName : $(gum style 6 "$(sudo scutil --get LocalHostName)")
EOM
    gum executed
  fi
fi

#==================================================[ Apps ]==================================================#

(! isDarwin) && export HOMEBREW_BUNDLE_BREW_SKIP='5ouma/formula/mksei blacktop/tap/lporg'
if (! gum spin "Satisfying Brewfile's dependencies..." -- brew bundle check --no-upgrade --file="$datas/Brewfile"); then
  gum format '## üì≤ Installing apps and commands...'
  brew bundle install --no-lock --no-upgrade --file="$datas/Brewfile"
  gum executed
fi
if (isDarwin) && [[ ! -d '/Applications/DaVinci Resolve' ]]; then
  gum format '### üé® Please install DaVinci Resolve.'
  sleep 2
  open 'https://www.blackmagicdesign.com/jp/products/davinciresolve'
  gum confirm 'Continue when you done.' 'Done' ''
  gum executed
fi

if (isDarwin); then
  lporg save -c="$TMPDIR/lporg.yml" >/dev/null 2>&1
  if (! diff "$datas/lporg.yml" "$TMPDIR/lporg.yml" >/dev/null 2>&1); then
    gum format '## üöÄ Setting up Launchpad and Dock...'
    # shellcheck disable=SC2016
    gum spin 'Running `lporg load`...' -- lporg load -nyc="$datas/lporg.yml"
    gum executed
  fi
fi

if (isDarwin) && [[ -n "$(mackup -n backup 2>/dev/null)" && $? ]]; then
  gum format '## üíæ Restoring appliaction settings...'
  mackup restore
  gum executed
fi

if (isDarwin && ! defaults read com.apple.universalaccess 'com.apple.custommenu.apps' >/dev/null 2>&1); then
  gum format '## ‚å®Ô∏è Restoring App Shortcuts...'
  # shellcheck disable=SC2016
  gum spin 'Running `mksei load`...' -- mksei load "$datas/keyboard_shortcuts.json"
  gum executed
fi

eval "$(rtx activate -q zsh)"
if [[ -n "$(rtx ls --missing)" ]]; then
  gum format '## üìº Installing programming languages...'
  rtx install
  gum executed
fi

if [[ ! -d "$XDG_DATA_HOME/vim/pack/jetpack/opt" ]]; then
  gum format '## üñå Installing Vim plugins...'
  export MYVIMRC="$XDG_CONFIG_HOME/vim/vimrc"
  export VIMINIT="source $MYVIMRC"
  vim +Jetpack +qall
  gum spin 'Building a binary of vim-lumen...' -- vim +qall
  gum executed
fi

#==================================================[ Done! ]==================================================#

if ($executed); then
  gum format '# üéâ Setup complete!'
else
  gum format '# üßä Nothing has changed.'
fi
