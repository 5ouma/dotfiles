#!/usr/bin/env zsh

trap 'rm -f "$gumbin"' EXIT

#==================================================[ Variables ]==================================================#

declare -r githuburl="https://github.com/5ouma/dotfiles.git"
[ -z "$ZDOTDIR" ] && export -r ZDOTDIR="$HOME/.zsh"
[ -z "$dotfiles" ] && export -r dotfiles="$HOME/.dotfiles"
declare -r packages="$dotfiles/packages"
[ -z "$datas" ] && export -r datas="$dotfiles/datas"

declare executed=false
declare gothrough=false

#==================================================[ Functions ]==================================================#

getGum() {
  declare -r gumurl="https://github.com/charmbracelet/gum/releases/download/v0.10.0/gum_0.10.0_$(uname)_$(uname -m).tar.gz"
  declare -r gumthemeurl="https://raw.githubusercontent.com/5ouma/dotfiles/HEAD/datas/blue.json"
  declare -gr gumbin="$HOME/.cache/gum"

  if ! (type gum >/dev/null 2>&1); then
    printf "  üööüí® Now delivering..."
    mkdir -p "$HOME/.cache" && curl -sL "$gumurl" | tar -x -C "$HOME/.cache" "gum" && declare -gr gumcom="$gumbin"
    printf "\r                        "
  else
    # shellcheck disable=SC2155
    declare -gr gumcom=$(which gum)
  fi

  if [[ ! -e "$datas/blue.json" ]]; then
    # shellcheck disable=SC2155
    declare -gr gumtheme=$(curl -s "$gumthemeurl")
  else
    # shellcheck disable=SC2155
    declare -gr gumtheme=$(cat "$datas/${gumthemeurl/*\/}")
  fi
}
getGum

gum() {
  case $1 in
  confirm) $gumcom confirm "$2" --selected.background="#027AFF" --affirmative="$3" --negative="$4" ;;
  format) $gumcom format --theme=<(echo -E "$gumtheme") "${@:2}" ;;
  input) $gumcom input --header="$2" --placeholder="$3" "${@:4}" ;;
  spin) $gumcom spin --spinner.foreground="#027AFF" --title="$2" "${@:3}" ;;
  executed)
    echo && $gumcom format "‚úÖ Done!"
    executed=true
    ;;
  esac
}

cyan() {
  printf "\033[36m%s\033[m" "$@"
}

getdot() {
  if [[ ! -e "$dotfiles" ]]; then
    gum spin "Cloning the repository into \`~/.dotfiles\`..." -- git clone "$githuburl" "$dotfiles"
  else
    gum spin "Pulling the dotfiles repository..." -- git -C "$dotfiles" pull
  fi
}

checkSym() {
  declare -Ag packs
  while read -r pack; do
    declare file="$HOME/${pack#"$packages"/*/}"
    if [[ ! -e "$file" || -n $(diff "$pack" "$file") ]]; then
      packs[$file]=$pack
    fi
  done < <(find "$packages" -type f ! -name ".DS_Store")
  ((${#packs} > 0))
}

mkSym() {
  # shellcheck disable=SC2296
  for file in "${(k)packs[@]}"; do
    mkdir -p "$(dirname "$file")"
    ln -is "${packs[$file]}" "$file"
    echo "- $(cyan "~${file#~}")"
  done | gum format
}

#==================================================[ Welcome ]==================================================#

if [[ ! -x $0 ]]; then
  gum format <<EOM
# Welcome to üíª dotfiles setup!
> https://github.com/5ouma/dotfiles
---
EOM
fi

#==================================================[ Flags ]==================================================#

case "$1" in
"-h" | "--help")
  gum format <<EOM
## Usage: setup

> üíª The most minimal and powerful dotfiles ever.

## Flags:
-  -h, --help    Print help information
-  -y, --yes     Start without confirming
-  -s, --set     Set files to home directory
EOM
  exit
  ;;
"-y" | "--yes")
  gothrough=true
  ;;
"-s" | "--set")
  getdot && checkSym && mkSym
  exit
  ;;
esac

#==================================================[ Preperations ]==================================================#

# Ask to confirm
if ! $gothrough; then
  gum confirm "Let's start setup!" "Yes!" "No" || exit 1
fi

# Clone the repository
getdot

# Zsh setting
zstyle ":completion:*:commands" rehash 1

#==================================================[ Homebrew ]==================================================#

if ! (type brew >/dev/null 2>&1); then
  gum format "## üç∫ Installing Homebrew..."
  /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
  [ "$(uname -m)" = "x86_64" ] && eval "$(/usr/local/bin/brew shellenv)"
  [ "$(uname -m)" = "arm64" ] && eval "$(/opt/homebrew/bin/brew shellenv)"
  brew doctor
  gum executed
fi

#==================================================[ Configs ]==================================================#

mkdir -p "$HOME/.cache" "$HOME/.vim/undo" "$HOME/.ssh/git"
touch "$HOME/.hushlogin"
if checkSym; then
  gum format "## üîó Symlinking files..."
  mkSym
  gum executed
fi

if [[ ! -e "$HOME/.ssh/git/github.pub" || ! -e "$HOME/.ssh/git/github-school.pub" ]]; then
  gum format "## üìÉ Downloading SSH public keys..."
  curl -s https://github.com/5ouma.keys | head -n 1 > "$HOME/.ssh/git/github.pub"
  curl -s https://github.com/Souma-S.keys | head -n 1 > "$HOME/.ssh/git/github-school.pub"
  gum executed
fi

#==================================================[ defaults ]==================================================#

if ! (defaults read com.apple.dock springboard-columns >/dev/null 2>&1) || [[ $(defaults read com.apple.dock springboard-columns) != 9 || $(defaults read com.apple.dock springboard-rows) != 8 ]]; then
  gum format "## üñº Changing Launchpad size..."
  defaults write com.apple.dock springboard-columns -int 9
  defaults write com.apple.dock springboard-rows -int 8
  defaults write com.apple.dock ResetLaunchPad -bool TRUE
  gum executed
fi

killall Dock
declare -r spacecount=$((5 - $(defaults read com.apple.dock persistent-apps | grep "small-spacer-tile" -c)))
if ((spacecount > 0)); then
  gum format "## üö• Adding spaces on Dock..."
  for ((i = 0; i < spacecount; i++)); do
    defaults write com.apple.dock persistent-apps -array-add '{tile-type="small-spacer-tile";}'
  done
  gum executed
fi

if ! (defaults read com.apple.screencapture location >/dev/null 2>&1) || [[ "$(defaults read com.apple.screencapture location)" != "$HOME/Pictures/Screen Capture" ]]; then
  gum format "## üì∑ Changing screen capture path..."
  mkdir -p "$HOME/Pictures/Screen Capture"
  defaults write com.apple.screencapture location "$HOME/Pictures/Screen Capture"
  gum executed
fi

killall Dock
killall SystemUIServer

# shellcheck disable=SC2155
declare -r modelname="$(system_profiler SPHardwareDataType | grep "Model Name" | sed "s/.*: //")"
# shellcheck disable=SC2155
declare -r computername="$(id -F)'s $modelname" localhostname="$(id -F)s-${modelname// /-}"
if [[ "$(scutil --get ComputerName)" != "$computername" || "$(scutil --get HostName)" != "$computername" || "$(scutil --get LocalHostName)" != "$localhostname" ]]; then
  gum format "## üñ• Setting computer name..."
  gum spin "Setting ComputerName..." -- scutil --set ComputerName "$computername"
  gum spin "Setting HostName..." -- scutil --set HostName "$computername"
  gum spin "Setting LocalHostName..." -- scutil --set LocalHostName "$localhostname"
  sudo -v
  gum format <<EOM
- ComputerName : $(cyan "$(scutil --get ComputerName)")
- HostName : $(cyan "$(scutil --get HostName)")
- LocalHostName : $(cyan "$(sudo scutil --get LocalHostName)")
EOM
  gum executed
fi

#==================================================[ Apps ]==================================================#

if [[ $(gum spin "Satisfying Brewfile's dependencies..." --show-output -- brew bundle check --file="$datas/Brewfile") != "The Brewfile's dependencies are satisfied." ]]; then
  gum format "## üì≤ Installing apps and commands..."
  brew bundle install --no-lock --file="$datas/Brewfile"
  gum executed
fi
if [[ ! -e "/Applications/DaVinci Resolve" ]]; then
  gum format "### üé® Please install DaVinci Resolve."
  sleep 2
  open "https://www.blackmagicdesign.com/jp/products/davinciresolve"
  gum confirm "Continue when you done." "Done" ""
  gum executed
fi

lporg save -c "$HOME/.cache/launchpad.yaml" >/dev/null 2>&1
if [[ -n $(diff "$datas/launchpad.yaml" "$HOME/.cache/launchpad.yaml") ]]; then
  gum format "## üñº Setting up Launchpad..."
  gum spin "Running \`lporg load\`" -- lporg load -n "$datas/launchpad.yaml"
  gum executed
fi
rm "$HOME/.cache/launchpad.yaml"

if [[ -n "$(mackup -n backup)" ]]; then
  gum format "## üíæ Restoring appliaction settings..."
  mackup restore
  gum executed
fi

if ! (defaults read com.apple.universalaccess "com.apple.custommenu.apps" >/dev/null 2>&1); then
  gum format "## ‚å®Ô∏è Restoring App Shortcuts..."
  gum spin "Running \`mksei load\`" -- mksei load "$datas/keyboard_shortcuts.json"
  gum executed
fi

eval "$(rtx activate -q zsh)"
if [[ "$(rtx ls)" =~ "missing" ]]; then
  gum format "## üìº Installing programming languages..."
  rtx install
  gum executed
fi

if [[ ! -e "$HOME/.vim/pack/jetpack/opt" ]]; then
  gum format "## üñå Installing Vim plugins..."
  [ -z "$VIMRC" ] && export -r VIMRC="$HOME/.vim/.vimrc"
  [ -z "$VIMINIT" ] && export -r VIMINIT="source $VIMRC"
  vim +Jetpack +qall
  gum executed
fi

#==================================================[ Done! ]==================================================#

if ($executed); then
  gum format "# üéâ Setup complete!"
else
  gum format "# üßä Nothing has changed."
fi
